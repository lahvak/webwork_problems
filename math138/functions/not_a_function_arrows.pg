# DESCRIPTION
# Arrow diagrams for functions
# written by Jan Hlavacek (jhlavace@svsu.edu)
# ENDDESCRIPTION

## DBsubject(Algebra)
## DBchapter(Functions)
## DBsection(Definition, concept)
## Institution(SVSU)
## Author(Jan Hlavacek)
## Level(2)
## TitleText1('Algebra and Trigonometry')
## AuthorText1('Swokowski, Cole')
## EditionText1('13')
## Section1('.')
## Problem1('')
## KEYWORDS('algebra,domain,diagram,function')

DOCUMENT();
loadMacros(
"PGstandard.pl",
"MathObjects.pl",
"PGML.pl",
"parserOneOf.pl",
"PGgraphmacros.pl",
"PGcourse.pl",
);

$showPartialAnswers = 1;

Context("Numeric");

@inputs = ();
foreach my $i (0..4) {
    do {$x = random(-20,20);} while (grep { $x == $_} @inputs);
    push @inputs, $x;
}

$double_idx = random(0,4);
$double = $inputs[$double_idx];
push @inputs, $double;

@rangelist = ();
foreach $_ (0..4) {
    do {$x = random(-20,20);} while (grep { $x == $_} @rangelist);
    push @rangelist, $x;
}

@output_is = map {random(0,4)} 0..4;

@outputs = map {$rangelist[$_]} @output_is;

$fst_double_output = $outputs[$double_idx];
do {$snd_double_output = random(-20,20);} while ($snd_double_output == $fst_double_output);
push @outputs, $snd_double_output;

do {$extra = random(-20,20);} while (grep { $extra == $_} (@outputs));

$input_to_remove = Compute("$double");
$output_to_remove = OneOf($fst_double_output, $snd_double_output);

sub arrow_diagram {
    my ($ref1, $ref2, $ref3, $ref4) = @_;
    my @inputs = @{$ref1};
    my @outputs = @{$ref2};
    my @extra_inputs = @{$ref3};
    my @extra_outputs = @{$ref4};
    my $lastind = min(scalar(@inputs),scalar(@outputs)) - 1;
    @inputs = @inputs[0..$lastind];
    @outputs = @outputs[0..$lastind];
    
    my %input_outputs;
    for my $i (0..$lastind){
        my $inp = $inputs[$i];
        $input_outputs{$inp} = [] unless exists $input_outputs{$inp};
        push @{$input_outputs{$inp}}, $outputs[$i];
    }

    @inputs_sorted = PGsort( sub {$_[0] < $_[1]}, (keys %input_outputs, @extra_inputs));
    @outputs_sorted = PGsort( sub {$_[0] < $_[1]}, (@outputs, @extra_outputs));

    my %output_indices;
    my $j = 0;
    for my $o (@outputs_sorted) {
        if (not exists $output_indices{$o}) {
            $output_indices{$o} = $j;
            $j += 1;
        }
    }

    @outputs_sorted = PGsort( sub {$_[0] < $_[1]}, keys %output_indices);

    my $inputno = scalar @inputs_sorted;
    my $outputno = scalar @outputs_sorted;

    my $inputgap = int(100/($inputno + 2));
    my $outputgap = int(100/($outputno + 2));

    my $yrad = max(100 - $inputgap, 100 - $outputgap)/2;


    # Create a graph and add the function to the graph

    my $graph = init_graph(0,0,100,100);

    # draw the ovals
    my $xfunc1 = sub { my $t = shift();
        return 25 + 15*cos($t); };
    my $xfunc2 = sub { my $t = shift();
        return 75 + 15*cos($t); };
    my $yfunc = sub { my $t = shift();
        return 50 + $yrad*sin($t); };
    my $fn1 = new Fun( $xfunc1, $yfunc, $graph );
    my $fn2 = new Fun( $xfunc2, $yfunc, $graph );
    $fn1->domain(0,6.2832);
    $fn2->domain(0,6.2832);

    for my $i (0..($inputno-1)) {
        $graph->stamps(
            closed_circle(25, 100 - 1.5*$inputgap - $inputgap*$i, "blue"),
        );
    }

    for my $i (0..($outputno-1)) {
        $graph->stamps(
            closed_circle(75, 100 - 1.5*$outputgap - $outputgap*$i, "blue"),
        );
    }

    for my $i (0..($inputno-1)) {
        my $li = new Label(23,100-1.5*$inputgap - $inputgap*$i,"$inputs_sorted[$i]", 'blue','right','middle','giant');
        $graph->lb($li);
    }

    for my $i (0..($outputno-1)) {
        my $lo = new Label(77,100-1.5*$outputgap - $outputgap*$i,"$outputs_sorted[$i]", 'blue','left','middle','giant');
        $graph->lb($lo);
    }

    for my $i (0..($inputno-1)) {
        my $in = $inputs_sorted[$i];
        my @outs = @{$input_outputs{$in}};
        for my $o (@outs) {
            my $j = $output_indices{$o};
            $graph->moveTo(25,100 - 1.5*$inputgap - $inputgap*$i);
            $graph->arrowTo(75, 100 - 1.5*$outputgap - $outputgap*$j);
        }
    }

    return $graph;
}

$graph = arrow_diagram(~~@inputs, ~~@outputs, [], [$extra]);

TEXT(beginproblem());
BEGIN_PGML
The following arrow diagram represents a relation that is not a function.

[@ image(insertGraph($graph),tex_size => 300, width=> 300, height=> 300) @]*

However, it is possible to turn this into a diagram of a function by removing an arrow that starts at [_____]{$input_to_remove} and ends at [____]{$output_to_remove}.
END_PGML

# Solution:
BEGIN_PGML_SOLUTION
This relation is not a function, because the input [`[$input_to_remove]`] has has two arrows starting at it: one that associates it with the output [`[$fst_double_output]`] and one that associates it with [`[$snd_double_output]`].  Removing one of these arrows will turn the diagram into a diagram of a function.
END_PGML_SOLUTION

ENDDOCUMENT();
