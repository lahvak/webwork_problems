## DESCRIPTION
## Algebra: Simplify rational expression
# written by Jan Hlavacek (jhlavace@svsu.edu)
## ENDDESCRIPTION


## DBsubject(Algebra)
## DBchapter(Rational expression)
## DBsection(Simplifying rational expressions)
## Level(2)
## KEYWORDS('algebra', 'rational expressions', 'complex fractions')


###########################
#  Initialization

DOCUMENT();

loadMacros(
"PGstandard.pl",
"MathObjects.pl",
"PGcourse.pl",
"unionTables.pl",
"parserMultiAnswer.pl",
"contextInequalities.pl",
"PGML.pl",
"AnswerFormatHelp.pl",
);

TEXT(beginproblem());


###########################
#  Setup

Context("Numeric")->variables->are(x=>"Real");
Context()->{error}{msg}{"Operands of '*' can't be words"} = " ";

$a = random(1,11);
do {$b = random(2,12);} until (gcd($b,$a) == 1);
do {$c = random(2,12);} until (gcd($c,$a) == 1 && $c != $b);
do {$d = random(2,12);} until (gcd($d,$c) == 1 && gcd($d,$b) == 1);

$p = list_random(-1,1)*random(1,9);
do {$q = list_random(-1,1)*random(1,9)} until ($q != $p);
do {$r = list_random(-1,1)*random(1,9)} until ($r != $p && $r != $q);

$a1 = Formula("$a*x - $a*$p")->reduce;
$b1 = Formula("$b*x - $b*$q")->reduce;
$c1 = Formula("$c*x - $c*$p")->reduce;
$d1 = Formula("$d*x - $d*$r")->reduce;

$xmp = Formula("x - $p")->reduce;
$xmq = Formula("x - $q")->reduce;
$xmr = Formula("x - $r")->reduce;

$num = Formula("$a*$d*(x-$r)")->reduce;
$den = Formula("$b*$c*(x-$q)")->reduce;

$multians = MultiAnswer($num, $den)->with(
  singleResult => 0,
  allowBlankAnswers => 1,
  checker => sub {
      my ( $correct, $student, $self ) = @_;
      my ( $f1stu, $f2stu ) = @{$student};
      my ( $f1, $f2 ) = @{$correct};
 
      if ( ( $f1==$f1stu &&  $f2==$f2stu) || 
           (-$f1==$f1stu && -$f2==$f2stu) ) {
          return [1,1];
      } elsif ( $f1==$f1stu || -$f1==$f1stu) {
          return [1,0];
      } elsif ( $f2==$f2stu || -$f2==$f2stu ) {
          return [0,1];
      } elsif ( $f1*$f2stu==$f1stu*$f2 ) {
          $self->setMessage(1,"Simplify your answer further");
          $self->setMessage(2,"Simplify your answer further");
          return [0,0];
      } else {
          return [0,0];
      }
  }
);


Context(Inequalities);

$restrictions = List("x != $p, x != $r");

# 
#  Display the fraction and answer blanks nicely
#
$blankwidth = 15;
Context()->texStrings;
$fraction1 = "\frac{$a1}{$b1}";
$fraction2 = "\frac{$c1}{$d1}";
if ($displayMode eq 'TeX') {
  $showfraction =
  "\( \displaystyle \frac{$fraction1}{$fraction2} = \frac{".$multians->ans_rule(2*$blankwidth)."}{".$multians->ans_rule(2*$blankwidth)."}\), ".ans_rule(2*$blankwidth);
} else {
  $showfraction =
  ColumnTable("\( \displaystyle \frac{$fraction1}{$fraction2} = \)",
              ColumnTable(
              BeginTable(border=>0, spacing=>0, padding=>0).
              AlignedRow([ans_rule($blankwidth)], align=>CENTER, separation=>0).
              AlignedRow([$HR], align=>CENTER, separation=>0).
              AlignedRow([ans_rule($blankwidth)], align=>CENTER, separation=>0).
              EndTable(), ", ".ans_rule($blankwidth),
              indent=>0, separation=>0, valign=>"CENTER"),
              indent=>0, separation=>0, valign=>"CENTER")
}
Context()->normalStrings;

###########################
#  Main text

BEGIN_PGML
Simplify as much as possible.  When entering domain restrictions, use [|!=|] for [`\neq`] and separate inequalities with commas. If there are no domain restrictions, enter NONE.

[$showfraction]***

END_PGML


############################
#  Answer evaluation

$showPartialCorrectAnswers = 1;

install_problem_grader(~~&std_problem_grader);

sub fix_plural_filter {
  my $ans = shift; 
  if ($ans->{ans_message} =~ /(.*) inequalitys(.*)/) {
      $ans->{ans_message} = "$1 inequalities$2";
  }
  return $ans; 
}

ANS( $multians->cmp() );
ANS( $restrictions->cmp()->withPostFilter(~~&fix_plural_filter));

############################
#  Solution

BEGIN_PGML_SOLUTION
[```
\frac{[$fraction1]}{[$fraction2]} = [$fraction1]\div[$fraction2] = [$fraction1]\cdot\frac{[$d1]}{[$c1]},\ x \neq [$r]
```]

Factoring and reducing the fractions:

[```
[$fraction1]\cdot\frac{[$d1]}{[$c1]},\ x \neq [$r] = \frac{[$a]([$xmp])}{[$b]([$xmq])}\cdot\frac{[$d]([$xmr])}{[$c]([$xmp])},\ x \neq [$r]
= \frac{[$num]}{[$den]},\ x \neq [$r],\ x \neq [$p]
```]

END_PGML_SOLUTION

ENDDOCUMENT();
